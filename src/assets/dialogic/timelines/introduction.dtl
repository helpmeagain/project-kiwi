if {introduction_part} == 1.0:
	jump introduction-1
elif {introduction_part} == 2.0:
	jump introduction-2
elif {introduction_part} == 3.0:
	jump introduction-3
elif {introduction_part} == 4.0:
	jump introduction-4
elif {introduction_part} == 5.0:
	jump introduction-5
elif {introduction_part} == 6.0:
	jump introduction-6
elif {introduction_part} == 7.0:
	jump introduction-7
elif {introduction_part} == 8.0:
	jump introduction-8
elif {introduction_part} == 9.0:
	jump introduction-9
kiwi: Hoje é o meu primeiro dia de aula...
kiwi: Estou tão ansiosa pelo que está por vir. Já consigo ouvir as borboletas no meu estômago!
kiwi: Será que cometi um erro com esse intercâmbio?
kiwi: ...
kiwi: Enfim, preciso terminar de me arrumar.
[style name="style"]
(Um tempo se passa...)
join kiwi (Sad) center [animation="Slide In Left" wait="true"]
kiwi: Okay. Acho que estou pronta. Eu consigo, eu sei que consigo!
kiwi (Neutral): Antes de sair, vou conversar com a minha irmã. Talvez ela consiga me tranquilizar.
leave kiwi [animation="Fade Out" wait="true"]
[signal arg="bg_dining_kitchen"]
[wait time="2.5"]
join kiwi (Neutral) left [animation="Slide In Left" wait="true"]
kiwi: Ostrich? Pode vir aqui rapidinho?
ostrich: Claro! Já estou indo.
join ostrich (Smile) right [animation="Slide In Right" wait="true"]
ostrich: Bom dia! Dormiu bem?
kiwi (Sad): Não muito. Fiquei muito ansiosa nessa noite, não consegui dormir tanto quanto queria.
ostrich: Se não der certo, você pode rir e aprender com a situação. O que te deixa ansiosa?
kiwi: Sinceramente, sinto que meu inglês está enferrujado. Até as coisas mais simples me fugiram a cabeça!
ostrich (Neutral): E se você treinar um pouquinho do simple past comigo?
Deseja jogar o tutorial?
- Sim
	set {did_tutorial} = true
	kiwi (Smile): Claro! Revisar o contéudo sempre é útil!
	ostrich: Ótimo! Vamos lá!
	update kiwi (Neutral)
	ostrich: O simple past é o nosso equivalente do pretérito perfeito.
	ostrich (Smile): Ou seja, é um tempo verbal que se refere a *ações que aconteceram e já terminaram*!
	ostrich: São frases do tipo "Eu trabalhei ontem", que ficaria "I worked yesterday".
	ostrich: Veja que o verbo ficou diferente. Normalmente, você veria ele no Simple Present como "works".
	ostrich (Proud): Mas no simple past, adionamos o –ed. Desta forma, teremos "worked".
	ostrich: Vamos ver! Tente completar a frase\:
	set {introduction_part} = 1.0
	[signal arg="introduction-part-1"]
	[end_timeline]
	label introduction-1
	join kiwi left [animation="Fade In" length="0.3"]
	join ostrich right [animation="Fade In" length="0.3"]
	[wait time="0.3"]
	ostrich: Ótimo! Você entendeu muito bem!
	ostrich (Smile): Essa regra do sufixo -ed pode ser diferente. Vamos analisar um pouco melhor.
	ostrich: Para verbos que terminam em "e", apenas adicionamos um "-d". Como por exemplo "Loved".
	ostrich (Neutral): Para verbos que terminam em "y" e precedidos com vogal, adicionamos o sufixo "-ed". Como na palavra "Enjoyed" (Enjoy).
	ostrich: E para verbos que terminam em "y" e precedidos com consoante, adicionamos "ied". Como em "Cried" (Cry).
	kiwi (Sad): Me parece um pouco complexo...
	ostrich (Smile): Com prática fica fácil! Vamos tentar treinar de novo!
	set {wrong_answer} = 0
	label introduction-1-5
	set {introduction_part} = 2.0
	[signal arg="introduction-part-2"]
	[end_timeline]
	label introduction-2
	set {introduction_part} = 3.0
	[signal arg="introduction-part-3"]
	[end_timeline]
	label introduction-3
	set {introduction_part} = 4.0
	[signal arg="introduction-part-4"]
	[end_timeline]
	label introduction-4
	join kiwi left [animation="Fade In" length="0.3"]
	join ostrich right [animation="Fade In" length="0.3"]
	if {wrong_answer} == 0.0:
		kiwi (Smile): Entendi até agora, está bem fácil!
		ostrich: Que bom! Você arrasa!
	elif {wrong_answer} <= 2.0:
		kiwi: Acho que entendi!
		ostrich: Ótimo! Com o tempo você pega o jeito!
	else:
		kiwi (Sad): Bom, isso foi difícil...
		ostrich (Neutral): Quer tentar fazer os exercícios novamente?
		ostrich: Talvez você entende melhor a lógica!
		[style name="style"]
		Deseja fazer os exercícios novamente?
		- Sim
			set {wrong_answer} = 0
			set {introduction_part} = 0
			jump introduction-1-5
		- Não
			kiwi (Tongue): Acho que aprendi o suficiente.
			ostrich (Smile): Tudo bem! Vamo seguir enfrente então.
	set {wrong_answer} = 0
	update kiwi (Neutral)
	update ostrich (Neutral)
	ostrich: Certo! Agora para mais um regra dos verbos regulares!
	ostrich: Se terminar em uma consoante precedida por uma única vogal, duplique a consoante antes de adicionar "-ed".
	ostrich (Proud): Por exemplo, o verbo "stop". Sendo a última letra uma consoante, a penúltima uma vogal, e a antepenúltima uma consoante...
	ostrich (Smile): Teremos o verbo "stopped!"
	kiwi (Smile): Então... se o verbo terminar em consoante, vogal, consoante... Repetir a última letra antes de adicionar -ed?
	ostrich (Smile): Ótimo! Vamos colocar em prática!
	set {introduction_part} = 5.0
	[signal arg="introduction-part-5"]
	[end_timeline]
	label introduction-5
	set {introduction_part} = 6.0
	[signal arg="introduction-part-6"]
	[end_timeline]
	label introduction-6
	join kiwi left [animation="Fade In" length="0.3"]
	join ostrich right [animation="Fade In" length="0.3"]
	kiwi (Neutral): Okay! Mas você tinha dito verbos regulares. E como funciona com os verbos irregulares?
	ostrich: Os verbos irregulares são aqueles que não tem sufixo bem definido e podem variar bastante.
	ostrich: Neste caso, é necessário consultar um dicionário!
	kiwi (Smile): Eu lembro de um verbo irregular! O verbo "go" se transforma em "went"!
	ostrich (Big Smile): Exato! E por exemplo, o verbo "eat" se transforma em "ate"!
	ostrich (Neutral): Vamos praticar com um verbo irregular!
	set {introduction_part} = 7.0
	[signal arg="introduction-part-7"]
	[end_timeline]
	label introduction-7
	join kiwi left [animation="Fade In" length="0.3"]
	join ostrich right [animation="Fade In" length="0.3"]
	kiwi: Okay! Então isso é tudo?
	ostrich (Proud): Ainda não! Precisamo falar das formas negativas e interrogativas!
	kiwi (Smile): Ah! Na forma negativa colocamos a verbo "did not" depois so sujeito e deixamos o verbo no infinitivo!
	ostrich: Isso mesmo! Vale lembrar que também podemos contrair o "did not" para "didn't"!
	ostrich (Smile): Vou dizer uma frase na negativa do simple past. Veja se consegue transcrever!
	set {introduction_part} = 8.0
	[signal arg="introduction-part-8"]
	[end_timeline]
	label introduction-8
	join kiwi left [animation="Fade In" length="0.3"]
	join ostrich right [animation="Fade In" length="0.3"]
	ostrich: Ótimo! Agora para as interrogativas!
	ostrich: Para elas, você também irá usar o verbo no infinitivo, e colocar o verbo auxliar "did" no começo da frase.
	kiwi: Eu lembro! Por exemplo, a frase "You watched the movie" iria virar "Did you watch the movie?"
	kiwi (Smile): É bem fácil, apesar de ser diferente do português...
	ostrich: Ótimo! Para o útimo exercício!
	set {introduction_part} = 9.0
	[signal arg="introduction-part-9"]
	[end_timeline]
	label introduction-9
	join kiwi (Smile) left [animation="Fade In" length="0.3"]
	join ostrich (Big Smile) right [animation="Fade In" length="0.3"]
	ostrich: Ótimo! Você está lembrando de tudo!
	kiwi: Me sinto muito mais confiante! Obrigada!
- Não
	set {did_tutorial} = false
	kiwi (Neutral): Hmm... Acho que vou deixar passar.
	kiwi (Smile): Acho que é só ansiedade. Vou conseguir lembrar de tudo!
	ostrich (Smile): Ótimo, sua confiança já está voltando com tudo!
update kiwi (Neutral)
ostrich (Neutral): E lembre-se, você pode ver dicas. Com certeza será útil!
ostrich: Ah, mais uma coisa!
ostrich: Quer tomar um café? Para te deixar um pouco mais acordada?
ostrich: Talvez seu dia seja mais fácil com uma xícara de café!
[style name="style"]
Deseja tomar café? Se tomar café,  você receberá questões mais fáceis, como verbos mais simples e menos verbos irregulares.
- Aceitar café (questões mais fáceis)
	set {coffee} = true
	kiwi (Smile): Claro! Como negar com cafézinho? Thank you very much!
	ostrich (Smile): You're welcome, sister!
	(Um tempo se passa...)
	update kiwi (Neutral)
	update ostrich (Neutral)
- Recusar café (questões mais difíceis)
	set {coffee} = false
	kiwi: Vou recusar dessa vez.
	if {did_tutorial} == false:
		ostrich (Proud): Para algúem que se diz ansiosa, você está *super* confiante!
		ostrich: Gosto de te ver assim. Mas da próxima vez, vamos tomar um café e revisar juntas!
		ostrich (Big Smile): Eu adoro estudar com você!
		kiwi (Smile): Pode deixar!
	else:
		ostrich: Claro, eu entendo. As vezes é melhor fazer as coisas do jeito difícil.
		ostrich: Da próxima vez, vamos tomar café juntas!
		kiwi (Smile): Pode deixar!
kiwi (Smile): Tchau irmã, até mais tarde!
ostrich: Bye bye! See you soon!
leave kiwi [animation="Slide Out Right" length="1.5"]
leave ostrich [animation="Slide Out Left" length="1.5"]
[wait time="1.5"]
jump train-ride/
[end_timeline]
